func to_density_char(d)
    case
    when d > 8.0
        ret ' '
    when d > 4.0
        ret '.'
    when d > 2.0
        ret '*'
    else
        ret '+'
    end
end

func mandel_converge'(real, imag, iters, creal, cimag)
    ret iters if iters > 255.0 || real*real + imag*imag >= 4.0

    ret mandel_converge'(
        real*real - imag*imag + creal,
        2.0*real*imag + cimag,
        iters+1.0,
        creal,
        cimag
    )
end

func mandel_converge(real, imag)
    ret mandel_converge'(real, imag, 0.0, real, imag)
end

func step_to(var from, to, by, block)
    for from < to
        block(from)
        from += by
    end
end

func mandel'(xmin, xmax, xstep, ymin, ymax, ystep)
    ymin.step_to ymax, ystep do |y|
        xmin.step_to xmax, xstep do |x|
            mandel_converge(x, y).to_density_char.print
        end
        println("")
    end
end

func mandel(realstart, imagstart, realmag, imagmag)
    mandel'(realstart, realstart+realmag*78.0, realmag, imagstart, imagstart + imagmag*40.0, imagmag)
end

func main
    mandel(-2.3, -1.3, 0.05, 0.07)
end

